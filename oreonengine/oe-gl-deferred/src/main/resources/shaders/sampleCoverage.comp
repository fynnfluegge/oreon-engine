#version 430 core

layout (local_size_x = 16, local_size_y = 16) in;

layout (binding = 0, r8) uniform writeonly image2D sampleCoverageMaskImage;

layout (binding = 1, rgba32f) uniform readonly image2DMS worldPositionImage;

layout (binding = 2, rgba16f) uniform writeonly image2D lightScatteringMask_out;

layout (binding = 3, rgba16f) uniform readonly image2DMS lightScatteringMask_in;

layout (binding = 4, rgba16f) uniform writeonly image2D specularEmissionBloom_out;

layout (binding = 5, rgba16f) uniform readonly image2DMS specularEmissionBloom_in;

uniform int multisamples;

const float threshold = 1.0;

void main()
{
	ivec2 computeCoord = ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);
	
	// detect world position discontinuities
	float positionDiscontinuities = 0;
	vec3[8] positions; 
	for (int i=0; i<multisamples; i++){
		positions[i] = imageLoad(worldPositionImage, computeCoord,i).rgb; 
	}
	for (int i=0; i<multisamples-1; i++){
		positionDiscontinuities += distance(positions[i],positions[i+1]);
	}
		
	float coverageValue = 0;
	
	if(positionDiscontinuities > threshold){	
		coverageValue = 1.0;
	}

	vec4 lightScatteringMaskValue = vec4(0);
	vec4 specularEmissionBloomMaskValue = vec4(0); 
	bool lightScatteringAlphaFlag = false;

	if (coverageValue == 1.0){	

		for (int i=0; i<multisamples; i++){
			if (!lightScatteringAlphaFlag){
				lightScatteringMaskValue = imageLoad(lightScatteringMask_in, computeCoord, i).rgba;
				if (lightScatteringMaskValue.a == 1.0)
					lightScatteringAlphaFlag = true;
			}
			specularEmissionBloomMaskValue += imageLoad(specularEmissionBloom_in, computeCoord, i).rgba;
		}

		specularEmissionBloomMaskValue /= multisamples;
	}
	else{
		lightScatteringMaskValue = imageLoad(lightScatteringMask_in, computeCoord, 0).rgba;
		specularEmissionBloomMaskValue = imageLoad(specularEmissionBloom_in, computeCoord, 0).rgba;
	}
			  
	imageStore(sampleCoverageMaskImage, computeCoord, vec4(coverageValue,0,0,1));
	imageStore(lightScatteringMask_out, computeCoord, lightScatteringMaskValue);
	imageStore(specularEmissionBloom_out, computeCoord, specularEmissionBloomMaskValue);
}